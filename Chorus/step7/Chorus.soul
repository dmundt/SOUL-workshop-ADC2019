graph Chorus [[ main ]]
{
    input stream
    {
        float modulationRate  [[ name: "Rate",       min: 0.01,  max: 20.0,   init: 0.5, step: 0.01 ]];
        float modulationDepth [[ name: "Depth",      min: 0.0,   max: 100.0,  init: 25.0 ]];
        float modulationShape [[ name: "Shape",      min: 0.0,   max: 1.0,    init: 0.0 ]];
        float delayLength     [[ name: "Delay",      min: 1,     max: 50.0,   init: 10.0 ]];
        float dryLevel        [[ name: "Dry Level",  min: 0.0,   max: 100.0,  init: 100.0 ]];
        float wetLevel        [[ name: "Wet Level",  min: 0.0,   max: 100.0,  init: 50.0 ]];
    }

    input stream float audioIn;
    output stream float<2> audioOut;

    let
    {
        voice1          = ChorusVoice (0.0f, -1.0f);
        voice2          = ChorusVoice (0.5f,  1.0f);
        mixer           = DryWetMixer;
    }

    connection
    {
        dryLevel -> mixer.dryLevel;
        wetLevel -> mixer.wetLevel;

        modulationRate  -> voice1.modulationRate,   voice2.modulationRate;
        modulationDepth -> voice1.modulationDepth,  voice2.modulationDepth;
        modulationShape -> voice1.modulationShape,  voice2.modulationShape;
        delayLength     -> voice1.delayLength,      voice2.delayLength;
        audioIn         -> voice1.audioIn,          voice2.audioIn;

        audioIn          -> mixer.dryIn;
        voice1.audioOut  -> mixer.wetIn;
        voice2.audioOut  -> mixer.wetIn;
        
        mixer.audioOut  -> audioOut;
    }
}

// ---------------------------------------------------------------------
processor DryWetMixer
{
    input stream
    {
        float dryLevel        [[ name: "Dry Level",  min: 0.0,   max: 100.0,  init: 100.0 ]];
        float wetLevel        [[ name: "Wet Level",  min: 0.0,   max: 100.0,  init: 50.0 ]];
    }

    input stream float dryIn;
    input stream float<2> wetIn;

    output stream float<2> audioOut;

    void run()
    {
        loop
        {
            audioOut << (dryLevel * 0.01f * dryIn) + (wetLevel * 0.01f * wetIn);
            advance();
        }
    }
}

// ---------------------------------------------------------------------
graph ChorusVoice (float initialPhase, float panPosition)
{
    input stream float modulationRate  [[ name: "Rate",     min: 0.1, max:20.0,     init: 2.0, step: 0.1 ]];
    input stream float modulationDepth [[ name: "Depth",    min: 0.0, max:100.0,    init: 50.0 ]];
    input stream float modulationShape [[ name: "Shape",    min: 0,   max:1,        init:0, text: "Sine|Triangle"]];
    input stream float delayLength     [[ name: "Delay",    min: 1.0, max:50.0,     init: 10.0]];

    input stream float audioIn;
    output stream float<2> audioOut;

    let
    {
        oscillator = Oscillator (initialPhase);
        panner     = StereoPanner (panPosition);
        delay      = Delay;
    }

    connection
    {        
        modulationRate  -> oscillator.modulationRate;
        modulationDepth -> oscillator.modulationDepth;
        modulationShape -> oscillator.modulationShape;
        delayLength     -> delay.delayLength;

        audioIn             -> delay.audioIn;
        oscillator.audioOut -> delay.modulationIn;
        delay.audioOut      -> panner.audioIn;
        panner.audioOut     -> audioOut;
    }
}

// ---------------------------------------------------------------------
processor Delay 
{
    input stream float delayLength  [[ name: "Delay",    min: 1.0,   max:50.0,   init: 10.0]];

    input stream float audioIn, modulationIn;
    output stream float audioOut;

    float[10000] buffer;

    void run()
    {
        let samplesPerMs = float (processor.frequency / 1000.0);

        wrap<buffer.size> writePos;

        loop
        {
            let delayMs = delayLength * samplesPerMs * (1.0f + modulationIn) + 1.0f;
            let delayedValue = readLinearInterpolated (buffer, float(writePos) - delayMs);
            buffer[writePos] = audioIn;

            audioOut << delayedValue;
            writePos++;
            advance();
        }
    }
}

// ---------------------------------------------------------------------
processor StereoPanner (float panPosition)
{
    input stream float audioIn;
    output stream float<2> audioOut;

    void run()
    {
        // panPosition -1, left, 1 right, 0 middle
        let leftLevel = (1 - panPosition) / 2.0f;
        let rightLevel = 1.0f - leftLevel;

        loop
        {
            audioOut << float<2> (leftLevel * audioIn, rightLevel * audioIn);
            advance();
        }
    }
}

// ---------------------------------------------------------------------
processor Oscillator (float initialPhase)
{
    input stream float modulationRate  [[ name: "Rate", min: 0.1, max:1000.0, init: 2.0, step: 0.1 ]];
    input stream float modulationDepth [[ name: "Depth", min: 0.0, max:100.0, init: 50.0 ]];
    input stream float modulationShape [[ name: "Shape", min: 0, max:1, init:0, text: "Sine|Triangle"]];

    output stream float audioOut;

    bool isSineWaveform (float modulationShape)
    {
        return modulationShape < 0.5f;
    }

    float getSineValue (float phase)
    {
        return sin (phase * float (twoPi));
    }

    float getTriangleValue (float phase)
    {
        return (phase < 0.5f) ? -1.0f + (phase * 4.0f) : 1.0f - ((phase - 0.5f) * 4.0f);
    }

    void run()
    {
        float phase = initialPhase;

        loop
        {
            let value = isSineWaveform (modulationShape) ? getSineValue (phase) : getTriangleValue (phase);

            audioOut << modulationDepth * 0.01f * value;

            phase = fmod (phase + modulationRate * float (processor.period), 1.0f);
            advance();
        }
    }
}

